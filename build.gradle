/*
 * Copyright 2019 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
    id = 'email.notifier'
    pluginVersion = '1.1.0'
    goCdVersion = '20.9.0'
    name = 'Email Notifier Plugin'
    description = 'Plugin to send email notifications'
    vendorName = 'ThoughtWorks, Inc.'
    vendorUrl = 'https://github.com/gocd-contrib/email-notifier'

    githubRepo {
        owner = System.getenv('GITHUB_USER') ?: 'bob'
        repo = 'email-notifier'
        token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
    }

    pluginProject = project

    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    assetsToRelease = [project.tasks.jar]
}

group = 'com.thoughtworks.go'
version = gocdPlugin.fullVersion(project)

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    deps = [
        gocdPluginApi: 'cd.go.plugin:go-plugin-api:22.3.0',
    ]

    versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
    compileOnly project.deps.gocdPluginApi
    implementation 'javax.mail:mail:1.4.7'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    testImplementation project.deps.gocdPluginApi
    testImplementation platform('org.junit:junit-bom:5.9.2')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation 'org.mockito:mockito-core:5.1.1'
    testImplementation('com.icegreen:greenmail:1.6.12') {
        exclude group: 'junit'
    }
    testImplementation 'org.slf4j:slf4j-simple:2.0.6'
}

test {
    useJUnitPlatform()
}

jar {
  from(configurations.runtimeClasspath) {
    into "lib/"
  }

  from(sourceSets.main.java) {
    into "/"
  }
}